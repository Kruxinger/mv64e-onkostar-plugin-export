plugins {
    id 'java-library'
    id("com.gradleup.shadow") version "8.3.8"
}

group = 'dev.pcvolkmer.onco'
version = '0.1.12'
description = 'OS Export-Plugin für das DNPM-Datenmodell 2.1'
java.sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenLocal()
    mavenCentral()
    // Ignore the next two maven repositories if you want to use a local build
    maven {
        url = uri("https://git.dnpm.dev/api/packages/public-snapshots/maven")
    }
    maven {
        url = uri("https://git.dnpm.dev/api/packages/public/maven")
    }

}

configurations {
    configureEach {
        resolutionStrategy {
            cacheChangingModulesFor(5, "minutes")
        }
    }
}

ext {
    onkostarVersion = '2.14.0'
    springVersion = '4.3.8.RELEASE'
    slf4jVersion = '1.7.2'
    junitVersion = '5.13.3'
    assertjVersion = '3.27.3'
    mockitoVersion = '5.18.0'
}

dependencies {
    implementation files("./libs/onkostar-api-${onkostarVersion}.jar")
    implementation "org.springframework:spring-beans:${springVersion}"
    implementation "org.springframework:spring-context:${springVersion}"
    implementation "org.springframework:spring-web:${springVersion}"
    implementation "org.springframework:spring-jdbc:${springVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation("dev.pcvolkmer.onco:mv64e-onkostar-data:0.1.1-SNAPSHOT") {
        changing = true
    }

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.3'
    testImplementation "org.springframework:spring-test:${springVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "ca.uhn.hapi:hapi-structures-v26:2.3"
    testImplementation "org.hibernate:hibernate-core:4.3.11.Final"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.shadowJar {
    archiveClassifier.set('all')

    // Relocations für Versionskonflikte
    relocate 'org.apache.commons.csv', 'dev.pcvolkmer.onco.shadow.commons.csv'
    relocate 'org.apache.commons.io', 'dev.pcvolkmer.onco.shadow.commons.io'
    relocate 'org.apache.commons.lang3', 'dev.pcvolkmer.onco.shadow.commons.lang3'
    relocate 'org.apache.commons.text', 'dev.pcvolkmer.onco.shadow.commons.text'
    relocate 'com.fasterxml.jackson', 'dev.pcvolkmer.onco.shadow.jackson'
    relocate 'com.google.common', 'dev.pcvolkmer.onco.shadow.guava'

    dependencies {
        // NUR das ausschließen, was Onkostar bereits hat
        exclude(dependency('org.springframework:.*'))
        exclude(dependency('org.slf4j:.*'))
        exclude(dependency {
            it.moduleGroup == 'de.itc.onkostar'
        })

        // ALLE anderen Dependencies automatisch inkludieren
        // (keine expliziten include-Regeln!)
    }

    mergeServiceFiles()
}
